buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion")
    }
}

group 'com.example'
version '1.0-SNAPSHOT'

apply plugin: 'groovy'
apply plugin: 'org.springframework.boot'

repositories {
    mavenCentral()
}

compileJava.doLast {
    def projectDir = project.projectDir.absolutePath
    def classesDir = compileJava.destinationDir
    def osName = System.getProperty("os.name").toLowerCase().contains('linux')? 'linux' : 'darwin'
    javaexec {
        main = 'org.bytedeco.javacpp.tools.Builder'
        classpath = files(configurations.javacpp.asPath)
        args = [
                '-cp', "$classesDir",
                "-Dplatform.includepath=$projectDir/src/main/cpp/include",
                "-Dplatform.linkpath=$projectDir/src/main/cpp/lib/$osName"
        ]
    }
}

configurations {
    javacpp
}

dependencies {
    javacpp 'org.bytedeco:javacpp:1.3.3'
    compile configurations.javacpp.dependencies
    compile 'org.springframework.boot:spring-boot-starter'
    compile 'org.codehaus.groovy:groovy-all:2.4.12'
    testCompile 'org.spockframework:spock-core:1.1-groovy-2.4'
    testCompile 'org.spockframework:spock-spring:1.1-groovy-2.4'
    testCompile 'org.springframework.boot:spring-boot-starter-test'
}

allprojects {
    apply plugin: 'idea'
    idea {
        module {
            outputDir file('build/classes/main')
            testOutputDir file('build/classes/test')
        }
    }
    if(project.convention.findPlugin(JavaPluginConvention)) {
        // Change the output directory for the main and test source sets back to the old path
        sourceSets.main.java.outputDir = new File(buildDir, "classes/main")
        sourceSets.test.java.outputDir = new File(buildDir, "classes/test")
        sourceSets.main.groovy.outputDir = new File(buildDir, "classes/main")
        sourceSets.test.groovy.outputDir = new File(buildDir, "classes/test")
        // for Gradle 3.0 or older version.
        //sourceSets.main.output.classesDir = new File(buildDir, "classes/main")
        //sourceSets.test.output.classesDir = new File(buildDir, "classes/test")
    }
}
